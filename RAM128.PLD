Name      RAM128;
Partno    00;
Date      06/02/24;
Revision  01;
Designer  Ralle Palaveev;
Company   None;
Assembly  None;
Location  None;
Device    g22V10;

/* This is for 128kB Saturn Card */

PIN 1      = RESET;
PIN 2      = RW;
PIN 3      = A15;
PIN 4      = A14;
PIN 5      = A13;
PIN 6      = A12;
PIN 7      = A3;
PIN 8      = A2;
PIN 9      = A1;
PIN 10     = A0;
PIN 11     = DevSel;
PIN 13     = F0;
PIN 14     = RA12;
PIN 15     = BANK;
PIN 16     = CE;
PIN 17     = WRRAM;
PIN 18     = OE;
PIN 19     = RDRAM;
PIN 20     = RA14;
PIN 21     = RA15;
PIN 22     = RA16;
PIN 23     = WE;

OE         = !RW;
WE         = !F0 # RW;
DEF        = A15 & A14 & (A13 # A12);
CE         = !(DEF & (RDRAM & RW # WRRAM & OE));
WRRAM      = (WRRAM & RESET & (DevSel # A0 # A2)) # !(DevSel # !A0 # A2);
RDRAM      = (RDRAM & RESET & (DevSel # !(A0 $ A1) # A2)) # !(DevSel # (A0 $ A1) # A2);
BANK       = (BANK & (DevSel # !A3 # A2)) # !(DevSel # A3 # A2) # !RESET;
RA12       = (BANK & !A13) # (A13 & A12);
RA14       = (RA14 & RESET & (A0 # DevSel # !A2)) # A0 & !DevSel & A2;
RA15       = (RA15 & RESET & (A1 # DevSel # !A2)) # A1 & !DevSel & A2;
RA16       = (RA16 & RESET & (A3 # DevSel # !A2)) # A3 & !DevSel & A2;

